import org.apache.commons.compress.archivers.sevenz.SevenZMethod

plugins {
    id 'java'
    id "io.freefair.compress.7z" version "8.6"
}

version = "Plus-2.7.4"
group = "ru.serega007.bcsc"
archivesBaseName = "BetterCSC"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly fileTree(dir: 'libs/lwjgl', include: ['*.jar'])
    compileOnly fileTree(dir: 'libs/MCLibs', include: ['*.jar'])
}

jar {
	from('./') {
		include('README.md')
	}
}

build.finalizedBy("customZip")

tasks.register('customZip', SevenZip) {
    from('build/libs/') {
        into 'Xenoceal/mods/'
    }
    from('Injector/') {
        include('bypass.jar')
        include('sunec.jar')
        into('Xenoceal/')
    }
    from('./') {
        include('README.md')
        rename { 'Инструкция.txt' }
    }
    archiveFileName = 'BetterCSC-' + version + '.7z'
    contentCompression = SevenZMethod.LZMA2
}