import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

version = System.getenv("VERSION") ? System.getenv("VERSION") : "test"
group = "ru.serega007.bcsc"
archivesBaseName = "BetterCSC-Plus"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly fileTree(dir: 'libs/lwjgl', include: ['*.jar'])
    compileOnly fileTree(dir: 'libs/MCLibs', include: ['*.jar'])
}

jar {
	from('./') {
		include('README.md')
	}
}

tasks.register('processSource', Sync) {
    from sourceSets.main.java
    inputs.property 'version', version
    filter(ReplaceTokens, tokens: [version: version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources) {
        filter(ReplaceTokens, tokens: [version:  version])
    }
    into(buildDir)
}